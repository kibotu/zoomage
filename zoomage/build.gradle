apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    namespace "com.jsibbold.zoomage"

    compileSdk compileSdkVer
    buildToolsVersion = buildToolsVer

    defaultConfig {
        minSdk minSdkVer
        targetSdk targetSdkVer

        versionCode VERSION_CODE
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // region lint options

    lint {
        disable 'TypographyFractions', 'TypographyQuotes', 'InvalidPackage'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        checkOnly 'NewApi', 'InlinedApi'
        quiet true
        abortOnError false
        ignoreWarnings true
        checkDependencies true
        ignoreTestSources true
        checkAllWarnings false
    }

    // endregion

    // region java compile options

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // endregion

    // region kotlin compile options

    kotlinOptions {
        jvmTarget = "17"
        allWarningsAsErrors = false
        kotlinOptions.verbose = true

        //## Hint: https://kotlinlang.org/docs/reference/java-to-kotlin-interop.html#default-methods-in-interfaces
        //## Hint: "-Xjvm-default=all-compatibility" and "-Xjvm-default=all-compatibility" will also work.
        freeCompilerArgs += [
                "-Xjvm-default=all-compatibility",
                // disable linting for opt-in annotations
                "-opt-in=kotlin.RequiresOptIn"
        ]
    }

    // endregion

    // region exclude duplicated meta inf files

    packagingOptions {
        // DebugProbesKt.bin is used for java debugging (not needed for android)
        // Hint: https://github.com/Kotlin/kotlinx.coroutines/issues/2274
        exclude 'DebugProbesKt.bin'

        excludes += '/META-INF/{AL2.0,LGPL2.1}'
        // https://stackoverflow.com/a/61893957/1006741
        excludes -= "/META-INF/*.kotlin_module"
    }

    // endregion
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.7.0"
    testImplementation 'junit:junit:4.13.2'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.github.kibotu"
                artifactId = 'zoomage'
                version = VERSION_NAME
            }
        }
    }
}